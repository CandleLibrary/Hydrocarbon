@IMPORT ./symbols.hcg as sym


@IMPORT ./comments.hcg as cm


@IMPORT ./functions.hcg as fn


@IGNORE θws θnl

########################################################################################################################
######################################  PRODUCTION BODIES
########################################################################################################################

<> hcg_production_bodies → production_bodies

<> production_bodies > production_body                                  ↦return { env.body_count++, [$sym1]  }
            
        │ production_bodies  ( \│ │ \| )  production_body                     
                                                                    
                                                                    ↦return { env.body_count++, $sym1.push($sym3), $sym1 }

        │ production_bodies cm::comment                    
                                                                    
                                                                    ↦return { $sym1 }


<> production_body → force_fork? entries                              ↦r { new env.fn.body([$sym2], env, pos, undefined, !!$$sym1 ) }


<> entries → [ body_entries? fn::reduce_function? fn::error_function? ]
                                                                    ↦cstr{ 
                                                                        this.body = $sym1; 
                                                                        this.reduce = $sym2; 
                                                                    }                                

        │ sym::empty_symbol                                              
                                                                    ↦cstr{ 
                                                                           this.body = []; 
                                                                           this.reduce = null; 
                                                                    }

        │ $eof                                                  
                                                                    ↦cstr{ 
                                                                           this.reduce = null;  
                                                                           this.body = [$sym1]; 
                                                                    }


<> body_entries → condition_clause                                     
                                                                       ↦return{ env.body_offset = 0, [$sym1] }

       │ fn::function_clause                                     
                                                                       ↦return{ env.body_offset = 0, [$sym1] }

       │ body_entries fn::function_clause                     
                                                                     ↦return { env.body_offset = $sym1.length, $sym1.push($sym2), $sym1 }

       │ body_entries condition_clause                         
                                                                     ↦return { env.body_offset = $sym1.length, $sym1.push($sym2), $sym1 }

       │ body_entries sym::symbol                                 
                                                                     ↦return { env.body_offset = $sym1.length,  $sym1.push($sym2), $sym1 }

       │ \[ body_entries \]                                        
                                                                      ↦return { env.no_blank_index++, $sym2.map(e => (e.NO_BLANK = env.no_blank_index, e) ) }

        |  sym::symbol                                                ↦return{ env.body_offset = 0, [$sym1] }                                                               


<> force_fork >     \( τFORK \)                                       ↦r{ true }

<> condition_clause → 
          \( \EXC sym::condition_symbol_list \)                     ↦c{ this.IS_CONDITION = true; this.type = "exc"; this.sym = $sym3; this.offset = -1; }
       │ \( \ERR sym::condition_symbol_list \)                     ↦c{ this.IS_CONDITION = true; this.type = "err"; this.sym = $sym3; this.offset = -1; }
       │ \( \IGN sym::condition_symbol_list \)                     ↦c{ this.IS_CONDITION = true; this.type = "ign"; this.sym = $sym3; this.offset = -1; }
       │ \( \RST sym::condition_symbol_list \)                     ↦c{ this.IS_CONDITION = true; this.type = "rst"; this.sym = $sym3; this.offset = -1; }
       │ \( \RED sym::symbol \)                                    ↦c{ this.IS_CONDITION = true; this.type = "red"; this.sym = $sym3; this.offset = -1; }

