
@SYMBOL <> +>


@IMPORT ./production_bodies.hcg as pb


@IMPORT ./functions.hcg as fn


@IMPORT ./symbols.hcg as sym


@IMPORT ./comments.hcg as cm


@IGNORE θws θnl θtb

########################################################################################################################
######################################  PRODUCTIONS
########################################################################################################################

<> productions → production  										
															↦return {  
																(!($sym1.IMPORT_OVERRIDE || $sym1.IMPORT_APPEND)) ? 
																env.productions.push($sym1) : 0 
															}
	│ fn::referenced_function									
															↦return {  
																env.refs.set($sym1.id,$sym1), null 
															}
	│ productions production 	 							
															↦return {  
																$sym2.id = env.productions.length, 
																(!($sym2.IMPORT_OVERRIDE || $sym2.IMPORT_APPEND)) ? 
																env.productions.push($sym2) : 0, 
																env.productions 
															}
	│ productions cm::comment 	 							    
															↦return {  
																env.productions 
															}
	│ productions fn::referenced_function						
															↦return {  
																env.refs.set($sym2.id, $sym2), 
																$sym1 
															}
	│ ɛ 	
 
<> production → 
	
	\<>  sym::production_id production_start_symbol  pb::production_bodies  			
																		
															↦cstr{ 
																this.name = $sym2; 
																this.bodies = $sym4; 
																this.id = -1; 
																env.functions.compileProduction(this, env, pos);
															}

	# REDEFINED IMPORT production. Allows an imported production to be redefined entirely. 

	│ \<> sym::imported_production_symbol ↦{ env.host_lex = lex.copy(); env.prod_name = sym[sym.length - 1].name; } production_start_symbol pb::production_bodies

															↦cstr{ 
																this.IMPORT_OVERRIDE = true;
																this.name = $sym2; 
																this.bodies = $sym4; 
																this.id = -1; 
																env.functions.compileProduction(this, env, pos);
															}

	# APPENDED IMPORT production. Allows additional bodies to defined for productions imported from other files. 

	│ \+> sym::imported_production_symbol ↦{ env.host_lex = lex.copy(); env.prod_name = sym[sym.length - 1].name;} production_start_symbol  pb::production_bodies

															↦cstr{ 
																this.IMPORT_APPEND = true;
																this.name = $sym2; 
																this.bodies = $sym4; 
																this.id = -1; 
																env.functions.compileProduction(this, env, pos);
															}
	
<> production_start_symbol → ↦{ env.host_lex = lex.copy(); env.prod_name = sym[sym.length - 1] } \→ 
	│ ↦{ env.host_lex = lex.copy(); env.prod_name = sym[sym.length - 1] } \>